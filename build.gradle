apply plugin: 'java'

java {
	sourceCompatibility = JavaVersion.VERSION_25
	targetCompatibility = JavaVersion.VERSION_25
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
	}
}

test {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

jar {
	archiveBaseName = "generator"
	destinationDirectory = layout.buildDirectory.dir("..")
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	manifest {
		attributes "Main-Class": "com.leekwars.Main"
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

tasks.register('socketJar', Jar) {
	archiveFileName = "generator_socket.jar"
	destinationDirectory = layout.buildDirectory.dir("..")
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	manifest {
		attributes "Main-Class": "com.leekwars.Socket"
	}
	from sourceSets.main.output
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

assemble.dependsOn(tasks.named('socketJar'))

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'junit:junit:4.13.2'
	implementation project(':leekscript')
	implementation platform('tools.jackson:jackson-bom:3.0.3')
	implementation 'tools.jackson.core:jackson-databind'
	implementation 'tools.jackson.core:jackson-core'
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation 'org.postgresql:postgresql:42.7.5'
}